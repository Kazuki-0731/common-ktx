apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "org.jlleitschuh.gradle.ktlint-idea"
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'

group = 'com.github.sasa-nori'
version = '1.0.7'

android {
    compileSdkVersion 29
    //noinspection GradleDependency
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 29

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding = 'UTF-8'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.1.0'
    api 'androidx.constraintlayout:constraintlayout:1.1.3'
    api "androidx.coordinatorlayout:coordinatorlayout:1.1.0"
    api 'androidx.core:core-ktx:1.2.0'
    api 'androidx.fragment:fragment-ktx:1.2.4'
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    api 'com.android.support:support-annotations:28.0.0'

    api "com.squareup.moshi:moshi-kotlin:1.9.2"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.9.2"

    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5"

    api 'com.squareup.okhttp3:okhttp:3.14.7'

    api "com.squareup.retrofit2:retrofit:2.8.1"
    api "com.squareup.retrofit2:converter-moshi:2.8.1"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

// Build a jar with source files.
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// Build a jar with javadoc.
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

ktlint {
    version = "0.22.0"
    android = true
    reporters = [ReporterType.CHECKSTYLE]
    ignoreFailures = true

    filter {
        exclude("**/*test*/**")
    }
}

dokka {
    outputDirectory = "../doc"
}
